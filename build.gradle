plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'com.epicplayera10'
version = '1.0.13'

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://oss.sonatype.org/content/groups/public/" }

    // JGit
    maven { url "https://jitpack.io" }

    // ACF
    maven { url "https://repo.aikar.co/content/groups/aikar/" }

    // Okaeri
    maven { url "https://storehouse.okaeri.eu/repository/maven-public/" }

    // Lumine
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT"

    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.2-beta-r3-b'
    compileOnly "com.ticxo.modelengine:ModelEngine:R4.0.4"
    //compileOnly "com.ticxo.modelengine:api:R3.1.9"

    implementation 'eu.okaeri:okaeri-configs-yaml-snakeyaml:5.0.0-beta.5'
    implementation 'eu.okaeri:okaeri-configs-yaml-bukkit:5.0.0-beta.5'
    implementation 'eu.okaeri:okaeri-configs-validator-okaeri:5.0.0-beta.5'
    implementation 'eu.okaeri:okaeri-configs-serdes-commons:5.0.0-beta.5'
    implementation 'eu.okaeri:okaeri-configs-serdes-bukkit:5.0.2'

    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.8.0.202311291450-r'

    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
}

shadowJar {
    relocate("eu.okaeri", "com.epicplayera10.itemsaddercontentssync.libs.eu.okaeri")
    relocate("org.eclipse.jgit", "com.epicplayera10.itemsaddercontentssync.libs.org.eclipse.jgit")
    relocate("co.aikar.commands", "com.epicplayera10.itemsaddercontentssync.libs.acf")
    relocate("co.aikar.locales", "com.epicplayera10.itemsaddercontentssync.libs.locales")
}

assemble.dependsOn(shadowJar)

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
